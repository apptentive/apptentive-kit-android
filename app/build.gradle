buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.gms:google-services:4.3.14'
    }
}

plugins {
  id "co.riiid.gradle"
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'

// Set from Jenkins. Set by parent job and passed into template job.
def theVersionCode = System.getenv("BUILD_NUMBER") as Integer ?: 100
def theVersionName = extractSdkVersion()
def githubToken = System.getenv("GITHUB_TOKEN") ?: "ABC123"
def gitBranch = System.getenv("BRANCH_NAME") ?: "git branch --show-current".execute().text.trim()
def gitTag = getTagString(gitBranch, theVersionName, theVersionCode)

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "apptentive.com.app"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode theVersionCode
        versionName theVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    
    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            storeFile file('release.keystore')
            storePassword 'androidisbetter'
            keyAlias 'release'
            keyPassword 'androidisbetter'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'default'

    productFlavors {
        dev {
            applicationIdSuffix ".dev"
            buildConfigField "String", "APPTENTIVE_KEY", "\"ANDROID-ANDROID-DEV-c9c0b324114f\""
            buildConfigField "String", "APPTENTIVE_SIGNATURE", "\"98f5539e9310dc290394c68b76664e98\""
        }
        qa {
            applicationIdSuffix ".qa"
            buildConfigField "String", "APPTENTIVE_KEY", "\"ANDROID-ANDROID-QA-3c8ec6c92de4\""
            buildConfigField "String", "APPTENTIVE_SIGNATURE", "\"c84ee194bdad6e9d0b7f100616819be2\""
        }
    }

    buildTypes.each {
        it.buildConfigField "java.util.Date", "APP_BUILD_DATE", "new java.util.Date(" + System.currentTimeMillis() + "L)"
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':apptentive-core')
    implementation project(':apptentive-core-ui')
    implementation project(':apptentive-feedback')
    implementation project(':apptentive-enjoyment-dialog')
    implementation project(':apptentive-survey')
    implementation project(':apptentive-notes')
    implementation project(':apptentive-ratings')
    implementation project(':apptentive-message-center')
    implementation project(':apptentive-navigate-to-link')
    implementation project(':apptentive-in-app-review')
    implementation "androidx.appcompat:appcompat:$appcompat_library_version"
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "com.google.android.material:material:$material_design_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"
    implementation 'io.jsonwebtoken:jjwt:0.9.1'


    // Firebase Messaging (For Message Center Push Notifications)
    implementation 'com.google.firebase:firebase-messaging:23.1.0'

    testImplementation "androidx.test:core:$androidx_test_version"

    androidTestImplementation "androidx.test.ext:junit:$androidx_junit_ext_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core_version"

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += [
                '-opt-in=apptentive.com.android.util.InternalUseOnly',
        ]
    }
}

def extractSdkVersion() {
    def text = file('../apptentive-feedback/src/main/java/apptentive/com/android/feedback/Constants.kt').text
    def match = text =~ ~/SDK_VERSION\s*=\s*"(\d+\.\d+\.\d+)"/
    return match[0][1]
}

def getTagString(gitBranch, versionName, versionCode) {
    if (gitBranch == 'production') {
        return versionName
    } else {
        return "${versionName}-${gitBranch}+${versionCode}"
    }
}

task tag(type: Exec) {
    workingDir '.'
        
    commandLine 'git', 'tag', gitTag
}

tasks.getByName('githubRelease').mustRunAfter(tasks.getByName('assemble'))

github {
    owner = 'apptentive'
    repo = 'apptentive-kit-android-private'
    token = githubToken
    tagName = gitTag
    targetCommitish = gitBranch
    name = "v${gitTag}"
    body = """# Apptentive Android SDK
Release notes go here.
"""
    assets = [
        'app/build/outputs/apk/release/app-release.apk',
        'apptentive-core-ui/build/outputs/aar/apptentive-core-ui-release.aar',
        'apptentive-core/build/outputs/aar/apptentive-core-release.aar',
        'apptentive-enjoyment-dialog/build/outputs/aar/apptentive-enjoyment-dialog-release.aar',
        'apptentive-feedback/build/outputs/aar/apptentive-feedback-release.aar',
        'apptentive-navigate-to-link/build/outputs/aar/apptentive-navigate-to-link-release.aar',
        'apptentive-notes/build/outputs/aar/apptentive-notes-release.aar',
        'apptentive-ratings/build/outputs/aar/apptentive-ratings-release.aar',
        'apptentive-survey/build/outputs/aar/apptentive-survey-release.aar',
    ]
}

